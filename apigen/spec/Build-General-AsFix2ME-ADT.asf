equations

[equ-sort-to-lit]
  sort-to-lit(sort($Char*)) = at-literal(""" $Char* """)

[equ-build-asfix-attrs-empty]
  $AFModName = modname-to-lit($ModName)
  ===>
  build-asfix-attrs($ModName, , $ConsOption) = 
    attrs([id($AFModName)])

[equ-build-asfix-attrs-empty]
  $AFModName = modname-to-lit($ModName)
  ===>
  build-asfix-attrs($ModName, {}, $ConsOption) = 
    attrs([id($AFModName)])

[equ-build-asfix-attrs-not-empty-no-attrs]
  $AFModName = modname-to-lit($ModName),
  build-asfix-attr-list($Attr+, $ConsOption) = 
  ===>
  build-asfix-attrs($ModName, {$Attr+}, $ConsOption) = 
    attrs([id($AFModName)])

[equ-build-asfix-attrs-not-empty]
  $AFModName = modname-to-lit($ModName),
  build-asfix-attr-list($Attr+, $ConsOption) = $AFAttr+
  ===>
  build-asfix-attrs($ModName, {$Attr+}, $ConsOption) = 
    attrs([$AFAttr+,id($AFModName)])

[equ-build-asfix-attrs-not-empty]
  build-asfix-attr-list($Attr+, $ConsOption) = $AFAttr+
  ===>
  build-asfix-attrs({$Attr+}, $ConsOption) = 
    attrs([$AFAttr+])

[default-equ-build-asfix-attrs-empty]
  build-asfix-attrs($Attributes, $ConsOption) = no-attrs 

[equ-modname-to-lit]
  modname-to-lit(moduleid($Char*)) = at-literal(""" $Char* """)

[equ-build-asfix-attr-list-single]
  build-asfix-attr-list($Attr, $ConsOption) = 
    build-asfix-attr($Attr, $ConsOption)

[equ-build-asfix-attr-list-divide]
  build-asfix-attr($Attr, $ConsOption) = $AFAttr+1,
  build-asfix-attr-list($Attr+, $ConsOption) = $AFAttr+2
  ===>
  build-asfix-attr-list($Attr, $Attr+, $ConsOption) =
     $AFAttr+1,$AFAttr+2

[default-build-asfix-attr-list-divide]
  build-asfix-attr($Attr, $ConsOption) = $AFAttr*1,
  build-asfix-attr-list($Attr+, $ConsOption) = $AFAttr*2
  ===>
  build-asfix-attr-list($Attr, $Attr+, $ConsOption) = $AFAttr*1,$AFAttr*2

[equ-build-asfix-attr-left]        build-asfix-attr(left, $ConsOption)        =
                                     atr("left")
[equ-build-asfix-attr-right]       build-asfix-attr(right, $ConsOption)       =
                                     atr("right")
[equ-build-asfix-attr-nonassoc]       build-asfix-attr(non-assoc, $ConsOption)       =
                                     atr("non-assoc")
[equ-build-asfix-attr-assoc]       build-asfix-attr(assoc, $ConsOption)       =
                                     atr("assoc")
[equ-build-asfix-attr-bracket]     build-asfix-attr(bracket, $ConsOption)     =
                                     atr("bracket")
[equ-build-asfix-attr-constructor] build-asfix-attr(constructor, $ConsOption) =
                                     atr("constructor")
[equ-build-asfix-attr-memo]        build-asfix-attr(memo, $ConsOption)        =
                                     atr("memo")
[equ-build-asfix-attr-traverse]    build-asfix-attr(traverse, $ConsOption)    =
                                     atr("traverse")
[equ-build-asfix-attr-avoid]       build-asfix-attr(avoid, $ConsOption)       =
                                     atr("avoid")
[equ-build-asfix-attr-prefer]      build-asfix-attr(prefer, $ConsOption)      =
                                     atr("prefer")

[equ-build-asfix-attr-cons-remove]
  build-asfix-attr(cons($ATerm), cons-remove) =

[equ-build-asfix-attr-cons-expand]
  build-asfix-attr(cons($ATerm), cons-expand) = cons($ATerm)
